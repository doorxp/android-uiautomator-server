import java.text.SimpleDateFormat

/*
 * The MIT License (MIT)
 * Copyright (c) 2015 xiaocong@gmail.com
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE
 * OR OTHER DEALINGS IN THE SOFTWARE.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.gladed.androidgitversion' version '0.4.14'
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 36

    // version code history
    // 1: original version
    // 2: update all dependencies to latest
    // 6: input method, battery,rotation monitor

    def date = new Date()
    def sdf = new SimpleDateFormat("yyyyMMddHHmmss")
    def formattedDate = sdf.format(date)

    androidGitVersion {
        baseCode formattedDate.toBigInteger()
    }
    namespace 'com.github.uiautomator'
    defaultConfig {
        applicationId "com.github.uiautomator"
        minSdkVersion 21
        targetSdkVersion 36
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword "uiautomator-release-2015"
            keyAlias "uiautomator"
            keyPassword "uiautomator"
        }
        debug {
            storeFile file("../release.keystore")
            storePassword "uiautomator-release-2015"
            keyAlias "uiautomator"
            keyPassword "uiautomator"
        }
    }

    applicationVariants.configureEach { variant ->
        def flavor = variant.productFlavors[0]
        def baseName = "app-uiautomator"
        outputs.configureEach { output ->
            outputFileName = "${baseName}.apk"
        }

        tasks.configureEach { task ->
            if (task.name == "bundle${variant.name.capitalize()}") {
                task.doLast {
                    def dir = "${getBuildFile().getParent()}/${flavor.name}/${buildType.name}"
                    def old_name = "${project.name}-${flavor.name}-${buildType.name}.aab"
                    def new_name = "${baseName}.aab"
                    file(dir + "/" + old_name).renameTo(dir + "/" + new_name)
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [includeMockPermission: true]
        }
        debug {
            signingConfig signingConfigs.debug
            manifestPlaceholders = [includeMockPermission: true]
        }
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    // fix try-with-resource warning
    // ref: https://stackoverflow.com/questions/40408628/try-with-resources-requires-api-level-19-okhttp
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }
    buildFeatures.aidl true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // server
    implementation libs.appcompat.v120
    implementation libs.nanohttpd
    implementation libs.okhttp
    implementation libs.commons.cli

    // test
    androidTestImplementation libs.runner
    androidTestImplementation libs.rules
    androidTestImplementation libs.core
    androidTestImplementation libs.annotation
    androidTestImplementation libs.uiautomator
    androidTestImplementation libs.jsonrpc4j
    androidTestImplementation libs.jackson.core
    androidTestImplementation libs.jackson.annotations
    androidTestImplementation libs.jackson.databind

    implementation project(':permission')
    androidTestImplementation libs.junit.v412
    androidTestImplementation libs.junit
}


repositories {
//    mavenCentral()
}
